/* SIE CONFIDENTIAL
PlayStation(R)4 Programmer Tool Runtime Library Release 05.008.001
* Copyright (C) 2011 Sony Interactive Entertainment Inc.
* All Rights Reserved.
*/

/* This is a standard Bezier patch evaluation DOMAIN_PATCH_TYPE shader.
 */

#include "../../../toolkit/shader_common/shader_base.h"
#include "../std_cbuffer.h"
#include "../sh_outputs.hs"


float3 bezier_eval( const float3 bezpatch[16], float4 polyU, float4 polyV )
{
    float3 res = 0;

    res  = polyV.x * ( bezpatch[0] * polyU.x + bezpatch[1] * polyU.y + bezpatch[2] * polyU.z + bezpatch[3] * polyU.w );
    res += polyV.y * ( bezpatch[4] * polyU.x + bezpatch[5] * polyU.y + bezpatch[6] * polyU.z + bezpatch[7] * polyU.w );
    res += polyV.z * ( bezpatch[8] * polyU.x + bezpatch[9] * polyU.y + bezpatch[10] * polyU.z + bezpatch[11] * polyU.w );
    res += polyV.w * ( bezpatch[12] * polyU.x + bezpatch[13] * polyU.y + bezpatch[14] * polyU.z + bezpatch[15] * polyU.w );
    
    return res;
}


// This is a subset of the Bernstein Polynomials. These are used for evaluation of a bezier patch surface.
float4 bern_polynomials(float t)
{
    return float4( (1-t)*(1-t)*(1-t), 3.0f*t*(1-t)*(1-t), 3.0f*t*t*(1-t), t*t*t );
}

// the derivative of each polynomial
float4 bern_polynomials_deriv(float t)
{
    return float4( -3*(1-t)*(1-t), 3*(1-t)*(1-t) - 6*t*(1-t), 6*t*(1-t) - 3*t*t, 3*t*t );
}

#ifdef REG_PATCHES_SIXTEEN_THREADS
	#define NR_OUT_CTRL_POINTS		16
#else
	#define NR_OUT_CTRL_POINTS		4
#endif

[DOMAIN_PATCH_TYPE("quad")]
VS_OUTPUT main( HS_CNST_OUTPUT input, float2 domainCoordinates : S_DOMAIN_LOCATION, const OutputPatch<HS_OUTPUT, NR_OUT_CTRL_POINTS> bezier_patch )
{
    VS_OUTPUT output = (VS_OUTPUT)0;

	float2 uv = domainCoordinates;
	
	// synthesize surface position
	float4 polyU = bern_polynomials( uv.x );
	float4 polyV = bern_polynomials( uv.y );

	
#ifdef REG_PATCHES_SIXTEEN_THREADS
	float3 positions[] = { bezier_patch[3].pos.xyz, bezier_patch[2].pos.xyz, bezier_patch[1].pos.xyz, bezier_patch[7].pos.xyz,
						   bezier_patch[13].pos.xyz, bezier_patch[0].pos, bezier_patch[4].pos, bezier_patch[6].pos.xyz, 
						   bezier_patch[14].pos.xyz, bezier_patch[12].pos, bezier_patch[8].pos, bezier_patch[5].pos.xyz, 
						   bezier_patch[15].pos.xyz, bezier_patch[9].pos.xyz, bezier_patch[10].pos.xyz, bezier_patch[11].pos.xyz};
#else	
	const float3 inner0 = float3(bezier_patch[0].corner.w, bezier_patch[0].edge_l.w, bezier_patch[0].edge_r.w);
	const float3 inner1 = float3(bezier_patch[1].corner.w, bezier_patch[1].edge_l.w, bezier_patch[1].edge_r.w);
	const float3 inner2 = float3(bezier_patch[2].corner.w, bezier_patch[2].edge_l.w, bezier_patch[2].edge_r.w);
	const float3 inner3 = float3(bezier_patch[3].corner.w, bezier_patch[3].edge_l.w, bezier_patch[3].edge_r.w);
	float3 positions[] = { bezier_patch[0].corner.xyz, bezier_patch[0].edge_l.xyz, bezier_patch[0].edge_r.xyz, bezier_patch[1].corner.xyz,
						   bezier_patch[3].edge_r.xyz, inner0, inner1, bezier_patch[1].edge_l.xyz, 
						   bezier_patch[3].edge_l.xyz, inner3, inner2, bezier_patch[1].edge_r.xyz, 
						   bezier_patch[3].corner.xyz, bezier_patch[2].edge_r.xyz, bezier_patch[2].edge_l.xyz, bezier_patch[2].corner.xyz};
#endif

	float3 surf_pos = bezier_eval( positions, polyU, polyV );

	// synthesize first order derivatives
	float4 deriv_polyU = bern_polynomials_deriv( uv.x );
	float4 deriv_polyV = bern_polynomials_deriv( uv.y );
	float3 sigmaU = bezier_eval( positions, deriv_polyU, polyV );
	float3 sigmaV = bezier_eval( positions, polyU, deriv_polyV );


	// build and normalize the surface normal
	float3 norm = normalize( cross(sigmaU, sigmaV) );


	output.vPosition = mul( float4(surf_pos,1), g_mLocToProj );
	output.pos.xyz = mul( float4(surf_pos,1), g_mLocToView).xyz;
	output.norm.xyz = mul( float4(norm,0), g_mLocToView).xyz;

	const float2 texC0 = input.texCoord[0];
	const float2 texC1 = input.texCoord[1];
	const float2 texC2 = input.texCoord[2];
	const float2 texC3 = input.texCoord[3];

	const float2 texST = texC0*(1-uv.x)*(1-uv.y) + texC1*uv.x*(1-uv.y) + texC2*uv.x*uv.y + texC3*(1-uv.x)*uv.y;
	output.pos.w = texST.x;
	output.norm.w = texST.y;
    
	return output;
}
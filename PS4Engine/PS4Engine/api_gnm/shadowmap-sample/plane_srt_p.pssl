/* SIE CONFIDENTIAL
PlayStation(R)4 Programmer Tool Runtime Library Release 05.008.001
* Copyright (C) 2013 Sony Interactive Entertainment Inc.
* All Rights Reserved.
*/

#include "std_srtstructs.h"
//#include "std_cbuffer.h"
#include "vsoutput.hs"
#include "../toolkit/shader_common/illum.hs"

float4 main( VS_OUTPUT In, SRT data : S_SRT_DATA ) : S_TARGET_OUTPUT
{
	float3 lightRgb = data.constants->m_lightColor.xyz;
	float4 lightAtten = data.constants->m_lightAttenuation;
	float3 ambientRgb = data.constants->m_ambientColor.xyz;
	float specPow = 20;

	// Calculate shadowing factor, modulate with attenuation
	float4 s4 = data.textures->shadowMap.GatherCmp(data.samplers->samp2, In.shadowMapPos.xy/ In.shadowMapPos.w, In.shadowMapPos.z/ In.shadowMapPos.w);
	// TODO: replace this spotlight term with something cheaper
	float3 shadowPos =  (2.0f * In.shadowMapPos.xyz / In.shadowMapPos.w) - 1.0;
	float s = (s4.x+s4.y+s4.z+s4.w)*.25f;
	s *= saturate(1.0 - pow(shadowPos.x*shadowPos.x + shadowPos.y*shadowPos.y, 8));

	// shading
	const float3 vN = In.vNorm;
	const float3 vT = In.vTang.xyz;
	const float3 vB = In.vTang.w * cross(vN, vT);
	float3 vL = data.constants->m_lightPosition.xyz - In.vPosInView;
	const float3 vV = normalize(float3(0,0,0) - In.vPosInView);
	float d = length(vL); vL = normalize(vL);
	float attenuation = saturate(1.0f/(lightAtten.x + lightAtten.y * d + lightAtten.z * d * d) - lightAtten.w);

	float4 normalGloss = data.textures->bumpGlossMap.Sample(data.samplers->samp1, In.TextureUV.xy);
	normalGloss.xyz = normalGloss.xyz * 2.0f - 1.0f;
	normalGloss.y = -normalGloss.y;		// normal map has green channel inverted

	float3 vBumpNorm = normalize(normalGloss.x*vT+normalGloss.y*vB+normalGloss.z*vN);
	float3 vGeomNorm = normalize(vN);

	float3 diff_col = data.textures->colorMap.Sample(data.samplers->samp0, In.TextureUV.xy).xyz;
	float3 spec_col = 2*(0.4*normalGloss.w+0.1);
	float3 vLightInts = s*attenuation*lightRgb*BRDF2_ts_nphong_nofr(vBumpNorm, vGeomNorm, vL, vV, diff_col, spec_col, specPow);
	vLightInts += (diff_col*ambientRgb);

	return float4(vLightInts, 1);
}
